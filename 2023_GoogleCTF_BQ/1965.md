
# 1965

## Intro

BASIC (Beginner's All-purpose Symbolic Instruction Code) was developed in 1964 at 
Dartmouth College by John G. Kemeny and Thomas E. Kurtz. It was designed to be a 
simple, easy-to-learn programming language for students and other non-specialists. 
BASIC was based on FORTRAN II, but with a number of simplifications and additions 
to make it more user-friendly.
                                                      
BASIC quickly became popular on mainframe computers, and in the mid-1970s, it 
became the standard programming language for microcomputers. This was due in 
part to the fact that Microsoft's Altair BASIC was one of the first programming 
languages available for the Altair 8800, one of the first personal computers.            

## Challenge 0

> An array and a loop helped to hide a flag, can you reverse this to get the flag?

```basic
010 PRINT "*** THE FLAG VERIFICATOR: LEVEL 1 ***"
020 PRINT "VERSION FOR MICROSOFT GW-BASIC 1983"
020 PRINT "FLAG FORMAT: FLAG{[A-Z0-9_!]*}"
040 PREFIX=5
050 FL=36
060 DIM F$(PREFIX+FL+1) : READ F$
110 U$=""
120 PRINT "FLAG"; : INPUT U$
130 IF LEN(U$)<>42 THEN GOTO 600
140 A$=""
150 FOR I=FL+PREFIX TO PREFIX+1 STEP -1
160 A$=A$+MID$(U$,I,1)
170 NEXT I
500 IF A$=F$ THEN PRINT "GOOD JOB! NOW SUBMIT THE FLAG." : END
600 PRINT "THE PROVIDED FLAG IS INVALID TRY ANONTHER" : GOTO 120
900 DATA MELBORP_A_EVLOS_OT_SDRAWKCAB_GNIKROW
```

The challenge description contained a remote server which ran `GW-BASIC` and allowed
us to edit and execute the code. After loading the challenge with `LOAD "CHALLENG"`, we could
use `LIST` to view the code or `EDIT <line number>` to edit a line. 

We can see that the program reads a string from the user at line `120`, and checks if its 
length is `42` (or aborts otherwise). The `42` seems to include the `PREFIX` (which is `FLAG{`)
and the flag length (i.e. `FL`, which is 36). Line `060` adds `1` to the calculation for the 
closing `}`.

Lines `150-170` look like a loop comparing the input to the expected data. The data
is stored at line `900` and is somehow "encrypted". We can see that the loop is pretty 
simple, comparing character by character with no extra manipulation. The catch is that
the loop works backwards, from the end of the string to the beginning. Let's reverse the
data to get the flag:

```python
>>> "MELBORP_A_EVLOS_OT_SDRAWKCAB_GNIKROW"[::-1]
'WORKING_BACKWARDS_TO_SOLVE_A_PROBLEM'
```

The flag: `FLAG{WORKING_BACKWARDS_TO_SOLVE_A_PROBLEM}`

## Challenge 1

> This one hides the flag somewhere in 2D. Can you retrieve the flag?

```basic
010 PRINT "*** THE FLAG VERIFICATOR: LEVEL 2 ***"
020 PRINT "VERSION FOR MICROSOFT GW-BASIC 1983"
030 PRINT "FLAG FORMAT: FLAG{[A-Z0-9_!]*}"
040 LET PREFIX=5
050 LET FL=33
110 U$=""
120 PRINT "FLAG"; : INPUT U$
130 IF LEN(U$)<>PREFIX+FL+1 THEN GOTO 600
200 LET R=3
210 LET C=11
220 LET F=PREFIX+1
230 FOR I=1 TO C
240 FOR J=1 TO R
250 LET X=(J-1)*C+I
260 FOR Z=1 TO X
270 READ A$
280 NEXT Z
300 RESTORE
310 IF A$<>MID$(U$,F,1) THEN GOTO 600
320 F=F+1
330 NEXT J
340 NEXT I
500 PRINT "GOOD JOB! NOW SUBMIT THE FLAG." : END
600 PRINT "THE PROVIDED FLAG IS INVALID TRY ANONTHER" : GOTO 120
900 DATA M,R,_,_,T,_,M,S,N,_,R
910 DATA A,I,I,A,W,D,E,I,A,A,A
920 DATA T,X,S,_,O,I,N,O,L,R,Y
```

We can see that each character from the flag gets compared to `A$` via a loop. 
Let's edit line `310` to print out the expected flag, character by character:

```
EDIT 310
310 PRINT A$;
RUN
*** THE FLAG VERIFICATOR: LEVEL 2 ***
VERSION FOR MICROSOFT GW-BASIC 1983
FLAG FORMAT: FLAG{[A-Z0-9_!]*}
FLAG? 123456789012345678901234567890123456789
MATRIX_IS_A_TWO_DIMENSIONAL_ARRAYGOOD JOB! NOW SUBMIT THE FLAG.
Ok
```

The flag: `FLAG{MATRIX_IS_A_TWO_DIMENSIONAL_ARRAY}`

## Challenge 2

> They told me to learn the standard code and the hexadecimal number system to get the flag.

```basic
010 PRINT "*** THE FLAG VERIFICATOR: LEVEL 3 ***"
020 PRINT "VERSION FOR MICROSOFT GW-BASIC 1983"
030 PRINT "FLAG FORMAT: FLAG{[A-Z0-9_!]*}"
040 LET PREFIX=5
050 LET FL=41
060 LET SHIFT=&H80
110 DIM F(FL)
120 PRINT "FLAG"; : INPUT U$
130 IF LEN(U$)<>PREFIX+FL+1 THEN GOTO 600
200 FC$=MID$(U$,PREFIX+1,PREFIX+FL)
230 FOR I=1 TO FL
240 F(I)=ASC(MID$(FC$,I,1))+SHIFT
250 NEXT I
300 LET ID=0
330 FOR I=1 TO FL
340 READ A$
350 IX$=MID$(A$,3,1)
360 GOSUB 700
370 LET RES=ID*16
380 IX$=MID$(A$,4,1)
390 GOSUB 700
400 RES=RES+ID
410 IF RES<>F(I) THEN GOTO 600
430 NEXT I
500 PRINT "GOOD JOB! NOW SUBMIT THE FLAG." : END
600 RESTORE
610 PRINT "THE PROVIDED FLAG IS INVALID TRY ANONTHER" : GOTO 120
700 REM *** H2D CONVERTER ***
710 IF ASC(IX$) < 64 THEN ID=VAL(IX$) : RETURN
720 ON ASC(IX$)-64 GOTO 730,740,750,760,770,780
730 ID=10 : RETURN
740 ID=11 : RETURN
750 ID=12 : RETURN
760 ID=13 : RETURN
770 ID=14 : RETURN
780 ID=15 : RETURN
900 DATA &HD3,&HD4,&HC1,&HCE,&HC4,&HC1,&HD2,&HC4,&HDF,&HC3
910 DATA &HCF,&HC4,&HC5,&HDF,&HC6,&HCF,&HD2,&HDF,&HC9,&HCE
920 DATA &HC6,&HCF,&HD2,&HCD,&HC1,&HD4,&HC9,&HCF,&HCE,&HDF
930 DATA &HC9,&HCE,&HD4,&HC5,&HD2,&HC3,&HC8,&HC1,&HCE,&HC7
940 DATA &HC5
```

Again, we'll edit the program to print out the expected flag, character by character:

```
EDIT 410
410 PRINT RES;
RUN
*** THE FLAG VERIFICATOR: LEVEL 3 ***
VERSION FOR MICROSOFT GW-BASIC 1983
FLAG FORMAT: FLAG{[A-Z0-9_!]*}
FLAG? 1234567890123456789012345678901234567
 211  212  193  206  196  193  210  196  223  195  207  196  197  223  198  207
 210  223  201  206  198  207  210  205  193  212  201  207  206  223  201  206
 212  197  210  195  200  193  206  199  197 GOOD JOB! NOW SUBMIT THE FLAG.
Ok
```

This isn't quite what we expected. However careful inspection shows that line `240` adds
a `SHIFT` value to all characters: `0x80`. Let's edit the code again to subtract the shift
and convert to ASCII:

```
EDIT 410
410 PRINT CHR$(RES - &H80);
RUN
*** THE FLAG VERIFICATOR: LEVEL 3 ***
VERSION FOR MICROSOFT GW-BASIC 1983
FLAG FORMAT: FLAG{[A-Z0-9_!]*}
FLAG? 12345679801234567890123456789012345678901234657
STANDARD_CODE_FOR_INFORMATION_INTERCHANGEGOOD JOB! NOW SUBMIT THE FLAG.
Ok
```

The flag: `FLAG{STANDARD_CODE_FOR_INFORMATION_INTERCHANGE}`

## Challenge 3

> This last one offers the perfect cipher, can you decrypt the flag?

```basic
010 PRINT "*** THE FLAG VERIFICATOR: LEVEL 4 ***"
020 PRINT "VERSION FOR MICROSOFT GW-BASIC 1983"
030 PRINT "FLAG FORMAT: FLAG{[A-Z0-9_!]*}"
035 OPTION BASE 1
040 LET PREFIX=5
050 LET FL=60
060 LET SL=12
070 DIM S(SL)
080 DIM F(FL)
090 FOR I=1 TO SL
100 READ S(I)
105 NEXT I
110 U$=""
120 PRINT "FLAG"; : INPUT U$
140 IF LEN(U$)<>PREFIX+FL+1 THEN GOTO 600
150 FOR J=1 TO FL
160 READ F(J)
170 NEXT J
400 FOR I=1 TO INT(FL/SL)
410 FOR J=1 TO SL
420 K=SL*(I-1)+J
430 Z=S(J) XOR ASC(MID$(U$,K+PREFIX,1))
440 IF Z<>F(K) THEN GOTO 600
450 NEXT J
460 NEXT I
500 PRINT "GOOD JOB! NOW SUBMIT THE FLAG." : END
600 RESTORE
610 PRINT "THE PROVIDED FLAG IS INVALID TRY ANONTHER" : GOTO 90
900 DATA 72,69,76,76,79,71,89,78,86,65,69,76
905 REM *** ENCRYPTED DATA ***
910 DATA 24,0,30,10,10,4,13,17,5,4,6,30
920 DATA 13,6,21,19,24,14,13,6,9,4,29,15
930 DATA 4,16,31,5,25,2,6,10,31,18,15,25
940 DATA 6,6,24,5,0,9,6,15,24,5,26,3
950 DATA 6,0,19,24,6,10,28,17,6,0,1,109
```

As before, we'll edit the code to get the flag. Notice how the code XORs the user input
(`ASC(MID$(U$,K+PREFIX,1))`) with the key from `900` (`S(J)`) and stores the result in `Z`.
In the compares `Z` to the encrypted data (`F(K)`). So, to get the expected plaintext
character, we need `S(J) ^ F(K)`. We can do this easily with any scripting language but
we'll continue with BASIC:

```
EDIT 440
440 PRINT CHR$(F(K) XOR S(J));
RUN
*** THE FLAG VERIFICATOR: LEVEL 4 ***
VERSION FOR MICROSOFT GW-BASIC 1983
FLAG FORMAT: FLAG{[A-Z0-9_!]*}
FLAG? 123456879012345679801234567890123456789012345678901234567980123456
PERFECT_SECRECY_WITH_EXCLUSIVE_DISJUNCTION_AND_ONE_TIME_PAD!
GOOD JOB! NOW SUBMIT THE FLAG.
Ok
```

The flag: `FLAG{PERFECT_SECRECY_WITH_EXCLUSIVE_DISJUNCTION_AND_ONE_TIME_PAD!}`